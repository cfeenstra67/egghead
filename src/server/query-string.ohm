QueryString {
    Query = orQuery*

    and = caseInsensitive<"and">
    or = caseInsensitive<"or">
    not = caseInsensitive<"not">

    orQuery =
      | orQuery space+ or space+ andQuery -- or
      | andQuery

    andQuery =
      | andQuery space+ and space+ notQuery -- and
      | notQuery

    notQuery =
      | not space+ notQuery -- not
      | queryComp

    queryComp = 
      | "(" spaces (orQuery spaces)* ")"? -- parentheses
      | queryTerm

    queryTerm =
      | columnQuery
      | term

    columnIdent = (letter | "_") (letter | digit | "_")*

    operator = "gt" | "lt" | "ge" | "le" | "eq" | "ne"

    columnQuery = columnIdent ":" (operator ":")? term

    term =
      | stringLiteral
      | rawTerm

    rawTerm = 
      | ("!".."'" | "*".."~")+

    stringLiteral =
      "\"" doubleStringCharacter* "\""

    doubleStringCharacter (character) =
      ~("\"" | "\\") any -- nonEscaped
      | "\\" escapeSequence -- escaped

    escapeSequence =
      "\"" -- doubleQuote
      | "\\" -- reverseSolidus
      | "/" -- solidus
      | "b" -- backspace
      | "f" -- formfeed
      | "n" -- newline
      | "r" -- carriageReturn
      | "t" -- horizontalTab
      | "u" fourHexDigits -- codePoint

    fourHexDigits = hexDigit hexDigit hexDigit hexDigit
}
